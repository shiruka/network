plugins {
  id "java"
  id "checkstyle"
  id "maven-publish"
  id "signing"
  id "net.kyori.blossom" version "1.3.0"
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group "io.github.shiruka"

java {
  sourceCompatibility = JavaVersion.VERSION_16
  targetCompatibility = JavaVersion.VERSION_16
}

blossom {
  def constants = "src/main/java/io/github/shiruka/network/ConnectionType.java"
  replaceToken("@version@", project.version, constants)
}

tasks.withType(JavaCompiler) {
  options.encoding = "UTF-8"
  options.compilerArgs += ["-Duser.language=en", "-Duser.country=US"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier "sources"
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  javadoc.options.tags = ["todo"]
  javadoc.options.addBooleanOption("html5", true)
  classifier "javadoc"
  from javadoc.destinationDir
}

test {
  useJUnitPlatform()
}

checkstyle {
  configFile = rootProject.file("checkstyle.xml")
  checkstyleTest.enabled = false
}

repositories {
  mavenCentral()
  maven {
    url "https://jitpack.io"
  }
  mavenLocal()
}

dependencies {
  implementation "org.apache.logging.log4j:log4j-api:2.14.1"
  implementation "com.lmax:disruptor:3.4.4"
  implementation "org.apache.logging.log4j:log4j-iostreams:2.14.1"
  implementation "org.apache.logging.log4j:log4j-slf4j-impl:2.14.1"
  implementation "org.apache.logging.log4j:log4j-core:2.14.1"

  implementation "io.netty:netty-all:4.1.68.Final"
  implementation "com.google.guava:guava:30.1.1-jre"
  implementation "network.ycc:netty-raknet-common:0.8-SNAPSHOT"
  implementation "network.ycc:netty-raknet-server:0.8-SNAPSHOT"

  implementation "org.jetbrains:annotations:22.0.0"
  annotationProcessor "org.jetbrains:annotations:22.0.0"

  implementation "org.projectlombok:lombok:1.18.20"
  annotationProcessor "org.projectlombok:lombok:1.18.20"

  testImplementation "org.hamcrest:hamcrest-library:2.2"
  testImplementation "org.llorllale:cactoos-matchers:0.25"
  testImplementation "org.junit.jupiter:junit-jupiter:5.8.0"

  testImplementation "org.cactoos:cactoos:0.50"
  testImplementation "org.jetbrains:annotations:22.0.0"
  testImplementation "org.projectlombok:lombok:1.18.20"
  testImplementation "com.google.guava:guava:30.1.1-jre"
  testImplementation "org.apache.logging.log4j:log4j-core:2.14.1"
  testImplementation "org.projectlombok:lombok:1.18.20"
  testImplementation "org.mockito:mockito-core:3.12.4"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId project.name
      version project.version

      from components.java
      artifact tasks.sourcesJar
      artifact tasks.javadocJar
      pom {
        name = "Shiru ka Network"
        description = "A network library to handle RakNet packets."
        url = "https://shiruka.github.io/"
        licenses {
          license {
            name = "MIT License"
            url = "https://mit-license.org/license.txt"
          }
        }
        developers {
          developer {
            id = "portlek"
            name = "Hasan Demirta≈ü"
            email = "utsukushihito@outlook.com"
          }
        }
        scm {
          connection = "scm:git:git://github.com/shiruka/network.git"
          developerConnection = "scm:git:ssh://github.com/shiruka/network.git"
          url = "https://github.com/shiruka/network"
        }
      }
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenJava
}

nexusPublishing {
  repositories {
    sonatype()
  }
}
