plugins {
  id "java"
  id "checkstyle"
  id "maven-publish"
  id "signing"
  id "net.kyori.blossom" version "1.2.0"
  id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

group "io.github.shiruka"

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

blossom {
  replaceToken("@version@", project.version)
}

compileJava {
  options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier "sources"
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  javadoc.options.encoding = "UTF-8"
  javadoc.options.tags = ["todo"]
  javadoc.options.addBooleanOption("html5", true)
  classifier "javadoc"
  from javadoc.destinationDir
}

checkstyle {
  configFile = rootProject.file("checkstyle.xml")
  checkstyleTest.enabled = false
}

test {
  useJUnitPlatform()
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

repositories {
  mavenCentral()
  maven {
    url "https://jitpack.io"
  }
  mavenLocal()
}

dependencies {
  compileOnly "org.jetbrains:annotations:22.0.0"
  annotationProcessor "org.jetbrains:annotations:22.0.0"

  compileOnly "org.projectlombok:lombok:1.18.20"
  annotationProcessor "org.projectlombok:lombok:1.18.20"

  compileOnly "io.netty:netty-all:4.1.68.Final"
  compileOnly "com.google.guava:guava:30.1.1-jre"
  compileOnly "it.unimi.dsi:fastutil-core:8.5.6"
  compileOnly "it.unimi.dsi:fastutil:8.5.6"
  compileOnly "org.apache.logging.log4j:log4j-api:2.14.1"

  testImplementation "org.junit.jupiter:junit-jupiter:5.8.0"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId project.name
      version project.version

      from components.java
      artifact tasks.sourcesJar
      artifact tasks.javadocJar
      pom {
        name = "Shiru ka Network"
        description = "A network library to handle RakNet packets."
        url = "https://shiruka.github.io/"
        licenses {
          license {
            name = "MIT License"
            url = "https://mit-license.org/license.txt"
          }
        }
        developers {
          developer {
            id = "portlek"
            name = "Hasan Demirta≈ü"
            email = "utsukushihito@outlook.com"
          }
        }
        scm {
          connection = "scm:git:git://github.com/shiruka/network.git"
          developerConnection = "scm:git:ssh://github.com/shiruka/network.git"
          url = "https://github.com/shiruka/network"
        }
      }
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenJava
}

nexusPublishing {
  repositories {
    sonatype()
  }
}
